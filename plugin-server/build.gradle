/*
 * Copyright 2000-2020 JetBrains s.r.o.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.github.rodm.teamcity-server'

dependencies {
    agent project(path: ':plugin-agent', configuration: 'plugin')
    server project(':powershell-common')
    server project(':powershell-server')
}

ext {
    teamcityDownloadsDir = "$rootDir/downloads"
    serversDir = "$rootDir/servers"
    teamCityDir = hasProperty('TeamCityDir') ? property('TeamCityDir') : "$serversDir/TeamCity-${teamCityVersion}"
    teamCityDataDir = hasProperty('TeamCityDataDir') ? property('TeamCityDataDir') : "$rootDir/teamcity/data/" + teamCityVersion
    teamCityJavaHome = System.properties['java.home']
    javaVersion = hasProperty('plugin.java.version') ? property('plugin.java.version') : '1.8'
}

teamcity {
    version = teamCityVersion

    server {
		descriptor {
            name = 'powershell-runner'
            displayName = 'PowerShell Runner'
            version = project.version
            vendorName = 'JetBrains, s.r.o.'
            vendorUrl = 'http://www.jetbrains.com/'
            description = 'Support for running PowerShell scripts'
            useSeparateClassloader = true
        }

      files {
     	into('kotlin-dsl') {
          from(rootProject.projectDir) {
            include 'Powershell.xml'
          }
         }
      }
    
      environments {
        teamcityDev {
          downloadsDir = file(teamcityDownloadsDir) 
          version = teamCityVersion
          homeDir = file(teamCityDir)
          dataDir = file(teamCityDataDir)
          javaHome = file(teamCityJavaHome)
          serverOptions '-Xdebug'
          serverOptions '-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=5500'
          serverOptions '-Dteamcity.docker.runners=jetbrains_powershell'

          serverOptions '-Dteamcity.development.mode=true'
          serverOptions '-Dteamcity.development.shadowCopyClasses=true'
        }
      }  
    }
}

project.tasks.getByName('serverPlugin').version = ''
